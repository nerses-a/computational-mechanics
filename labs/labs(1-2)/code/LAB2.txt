FINISH
/CLEAR

L1 = 2 ! в метрах(работаем в СИ)
L2 = 1.5
A = 0.002
F1 = 10000
F2 = 20000
E = 2e11
MU = 0.3

/PREP7 ! Подключение библиотеки
ET,1,LINK180 ! Выбор конечного элемента

MP,EX,1,E ! Выбор материала
MP,PRXY,1,MU ! Коэффициент Пуассона

SECTYPE,1,LINK ! Создание поперечного сечения для типа LINK
SECDATA,A ! Площадь


! Строим узлы
N,1,0,0,0
N,2,L1,0,0
N,3,2*L1,0,0
N,4,3*L1,0,0
N,5,2.5*L1,L2,0
N,6,1.5*L1,L2,0
N,7,0.5*L1,L2,0

! Создание конечных элементов
E,1,2
E,2,3
E,3,4
E,4,5
E,5,3
E,3,6
E,5,6
E,6,2
E,6,7
E,2,7
E,7,1

! Для отображения узлов
/PNUM,NODE,1

! Для включения отображения толщины КЭ
/ESHAPE,1

! Обновляем 
GPLOT

! Закрепяем конструкцию
/SOL
D,1,UX,0
D,1,UY,0
D,4,UY,0
D,ALL,UZ,0

! Нагружаем конструкцию(добавляем силы)
F,7,FY,-F1
F,5,FX,F2

! Запуск расчетов
SOLVE

! Отображаем результаты
/POST1
!PLDISP,1 ! Отображения перемещений
PLESOL,S,X ! Отображения напряжений

