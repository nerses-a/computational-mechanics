FINISH
/CLEAR

! Начальные параметры
K = 1                  ! Жесткость пружины
M = 1                  ! Масса
L = 0.2                ! Длина

! Выбор КЭ

/PREP7                  ! Запуск библиотеки

! Элемент для сосредоточения масс
ET,1,MASS21,,,4

! Элемент для соединения пружины и демпфера
ET,2,COMBIN14,,,2

! Не требуется задавать свойства материала для этих элементов

! Задание параметров КЭ
R,1,M                  ! Задаем массу для КЭ1
R,2,K                  ! Задаем жесткость для КЭ2
!R,2,K,C               ! Задаем жесткость и демпфирование для КЭ2 (если необходимо)

! Моделирование

! Узлы
N,1,0,0,0               ! Создаем узел 1 в начале координат
N,2,L,0,0               ! Создаем узел 2 с координатами (L, 0, 0)
N,3,2*L,0,0             ! Создаем узел 3 с координатами (2L, 0, 0)

! Создание пружины
TYPE,2                   ! Выбираем тип элемента (2 - пружина)
REAL,2                   ! Устанавливаем свойства для элемента (2 - набор свойств)
E,1,2                    ! Соединяем узел 1 с узлом 2
E,2,3                    ! Соединяем узел 2 с узлом 3

! Создание масс
TYPE,1                   ! Выбираем тип элемента (1 - масса)
REAL,1                   ! Устанавливаем свойства для элемента (1 - набор свойств)
E,2                      ! Присоединяем массу к узлу 2
E,3                      ! Присоединяем массу к узлу 3

/ESHAPE,1                ! Устанавливаем форму элементов для более реалистичного отображения
GPLOT

! Закрепление
/SOL

! Заделка
D,1,ALL,0                ! Задаем условия нулевого перемещения для узла 1
! Шарниры
D,ALL,UY,0

! Не требуется задавать нагрузки

! Задаем задачу динамики
ANTYPE,2                  ! 2 - задача динамики (0 - задача статики по умолчанию)

! Метод решения уравнений (в большинстве случаев используется этот метод)
MODOPT,LANB,2,1e-3,1000

SOLVE                      ! Запуск расчета

/POST1                     ! Результаты

! Вывод списка собственных частот в Гц (возможно, будет отображаться только в консоли)
!SET,LIST

! Выбор интересующей нас собственной частоты
SET,,1                    ! 1 - для первой собственной частоты
PLDISP,1

! Для анимации
ANMODE,30,0.1

