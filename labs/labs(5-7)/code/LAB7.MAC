FINISH
/CLEAR

D1 = 30                ! Диаметр 1
D2 = 17                ! Диаметр 2
H1 = 25                ! Высота 1
H2 = 10                ! Высота 2
H3 = 40                ! Высота 3
R = 24                 ! Радиус
E = 2e5                ! Модуль упругости материала
MU = 0.3               ! Коэффициент Пуассона материала

! Выбор КЭ
/PREP7                  ! Запуск библиотеки

! Выбираем тип элемента (10-узловой тетраэдр)
ET,1,SOLID187

! Задаем свойства материала
MP,EX,1,E               ! Задаем модуль упругости материала
MP,PRXY,1,MU            ! Задаем коэффициент Пуассона материала

! Построение геометрии

! Построение плоской конструкции

! Точки

K,1,,0,0,0              ! Создаем узел 1 в начале координат
K,2,R/2,0,0             ! Создаем узел 2 с координатами (R/2, 0, 0)

! Устанавливаем полярную систему координат для точки 4
CSYS,1

! Задание координат R, PHI (в градусах), Z
K,4,R/2,60,0            ! Создаем узел 4 в полярных координатах

! Для точек 5 и 3 воспользуемся копированием (перемещением)
KGEN,2,2,,,(D1-R)/2     ! Копируем узел 2 (оригинал)
KGEN,2,4,,,(D1-R)/2     ! Копируем узел 4

! Построение линий

L,3,5                   ! Построение дуги (так как находимся в полярной системе)

CSYS,0                   ! Возвращаемся к декартовой системе координат

! Строим линии
L,1,2
L,1,4
L,4,5
L,2,3
L,2,4

! Присваиваем номера линиям
/PNUM,LINE,1
GPLOT

! Построение площадей через линии
AL,2,3,6
AL,1,4,5,6

! Показываем номера площадей
/PNUM,AREA,1
GPLOT

! Возвращаемся в полярную систему координат и копируем площади
CSYS,1
AGEN,6,ALL,,,,60
CSYS,0                   ! Возвращаемся в декартовую систему координат

! Не соединенные части разбиваем на 6 частей и объединяем
NUMMRG,ALL               ! Удаляем номера с большим номером
NUMCMP,ALL               ! Устраняем пропуски в нумерации

! Построим дополнительную линию
K,14,0,0,H2
K,15,0,0,H2+H3
L,14,15

! Операции выдавливания (2 способа)

! Нижний цилиндр

! 1 способ
ASEL,S,,,2,12,2
VEXT,ALL,,,0,0,-H1
ALLSEL

! Второй способ для конуса
VEXT,1,12,1,0,0,H2,D2/D1,D2/D1

! Верхний цилиндр

! 2 способ
ASEL,S,LOC,Z,H2
VDRAG,ALL,,,,,,25
ALLSEL

! Разбиваем на КЭ и задаем размер элементов
NUMMRG,ALL
AESIZE,ALL,D2/5

! Устанавливаем форму элементов для тетраэдров
MSHAPE,1,3D
MSHKEY,0

! Генерируем сетку
VMESH,ALL

