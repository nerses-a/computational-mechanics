FINISH
/CLEAR

L = 400                  ! Длина
B = 200                  ! Ширина
XR = 80                  ! Координата X центра круга
YR = 120                 ! Координата Y центра круга
R = 50                   ! Радиус круга
E = 0.72e5               ! Модуль упругости материала
MU = 0.25                ! Коэффициент Пуассона материала
Q = 10                   ! Заданное давление

! Подключение библиотеки
/PREP7

! Выбор конечного элемента
ET,1,PLANE183

! Материал
MP,EX,1,E               ! Задаем модуль упругости материала
MP,PRXY,1,MU            ! Задаем коэффициент Пуассона материала

! Построение геометрии

! Строим точки пятиугольника
K,1,0,0,0               ! Точка 1 с координатами (0, 0, 0)
K,2,0,B,0               ! Точка 2 с координатами (0, B, 0)
K,3,L,B,0               ! Точка 3 с координатами (L, B, 0)
K,4,L,YR+R,0            ! Точка 4 с координатами (L, YR+R, 0)
K,5,XR,0,0              ! Точка 5 с координатами (XR, 0, 0)

! Построение площади внутри пятиугольника
A,2,3,4,5,1              ! Построение площади (перечисление точек по порядку!)

! Построение круга
CYL4,XR,YR,R             ! Создание сплошного круга

! Операция вычитания
ASBA,1,2                 ! Вычитает из 1 фигуры (пятиугольник) 2 фигуру (круг)

! Разбиение на конечные элементы

!AATT                     ! Материал первый, элемент первый (в данном случае не нужна)

! Размеры КЭ
AESIZE,ALL,B/40

! Разбиение
AMESH,ALL

! Закрепление и нагрузки

/SOL

/PNUM,LINE,1              ! Просмотр номеров линий (так как не мы строили, не знаем номера)
LPLOT

! Закрепление

! Способ 1
DL,5,,ALL,0               ! Закрепляем 5 линию

! Способ 2
LSEL,S,LOC,x,0            ! Выбор всех линий с координатой X=0
DL,ALL,,ALL,0             ! Закрепление выбранных линий
ALLSEL

! Нагрузки

SFL,1,PRES,Q               ! Задаем давление
SBCTRAN                    ! Принудительный переход давлений с линий на КЭ
/PSF,PRES,NORM,2,0,1       ! Задаем нормальное распределенное давление
/PBC,ALL,,1                ! Чтобы не исчезли закрепления
GPLOT

! Расчет 
SOLVE

! Показ напряжений
/POST1
PLESOL,S,eqv

PLESOL,SDSG

