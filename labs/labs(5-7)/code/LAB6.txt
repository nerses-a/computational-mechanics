FINISH
/CLEAR

! Размеры в СИ
E = 2e11                ! Модуль упругости материала
MU = 0.3                ! Коэффициент Пуассона материала
R = 80e-3               ! Радиус
L = 80e-3               ! Длина
H = 2e-3                ! Толщина оболочки

/PREP7

! Выбор КЭ
ET,1,SHELL208           ! Выбор типа конечного элемента (SHELL208 - оболочка с 8 узлами)

! Материал
MP,EX,1,E               ! Задаем модуль упругости материала
MP,PRXY,1,MU            ! Задаем коэффициент Пуассона материала

! Толщина оболочки
SECTYPE,1,SHELL         ! Выбор типа сечения (оболочка)
SECDATA,H               ! Задаем толщину оболочки

! Ось вращения обязательно должна быть Y

! Построение точек
K,1,2*R,0,0             ! Точка 1 с координатами (2*R, 0, 0)
K,2,R,L,0               ! Точка 2 с координатами (R, L, 0)
K,3,R,2*L,0             ! Точка 3 с координатами (R, 2*L, 0)

! Построение геометрии
L,1,2                   ! Построение образующей конуса
L,2,3                   ! Построение образующей цилиндра

! Построение КЭ
LESIZE,ALL,L/20         ! Задаем размер элементов
LMESH,ALL               ! Генерация сетки
/ESHAPE,1               ! Установка формы элементов для лучшей визуализации
GPLOT

! Для 3D изображения (для просмотра, при моделировании неудобно использовать)
/EXPAND,27,AXIS,,,10    ! 27 * 10 показывает какую часть 360 градусов нарисовать (3/4)
/VIEW,1,1,1,1           ! Поворот в изометрии
GPLOT

! Закрепления и нагрузки

/SOL
DK,1,ALL,0               ! Закрепление узла 1 (все перемещения равны нулю)

! Давление жидкости
*DIM,P,TABLE,2,1,1,Y    ! Массив давлений в зависимости от координаты Y (линейная зависимость)

! Давление снизу
P(1,0) = 0
P(1,1) = 9800*2*L       ! Задаем давление в зависимости от координаты Y

! Давление сверху
P(2,0) = 2*L
P(2,1) = 0

SFL,ALL,PRES,%P%        ! Задаем давление из массива P

! Просмотр
/PBC,ALL,,1
SBCTRAN
/PSF,PRES,NORM,2,0,1
GPLOT

! Расчет 
SOLVE

! Просмотр напряжений
/POST1
PLESOL,s,eqv

